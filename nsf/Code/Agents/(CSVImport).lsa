<?xml version='1.0' encoding='utf-8'?>
<agent name='(CSVImport)' xmlns='http://www.lotus.com/dxl' version='9.0' replicaid='C1257BE1003E4E81'
 hide='web v3' runaswebuser='true' publicaccess='false' designerversion='8.5.3'>
<noteinfo noteid='592' unid='5C42B63009F5BEB7C1257BE40074358B' sequence='42'>
<created><datetime dst='true'>20130912T230918,83+02</datetime></created>
<modified><datetime dst='true'>20130913T002708,30+02</datetime></modified>
<revised><datetime dst='true'>20130913T002708,28+02</datetime></revised>
<lastaccessed><datetime dst='true'>20130913T002708,28+02</datetime></lastaccessed>
<addedtofile><datetime dst='true'>20130912T230918,91+02</datetime></addedtofile></noteinfo>
<updatedby><name>CN=Mark Leusink/O=LinQed</name></updatedby>
<wassignedby><name>CN=Mark Leusink/O=LinQed</name></wassignedby>
<designchange><datetime dst='true'>20130913T002725,12+02</datetime></designchange>
<trigger type='agentlist'/>
<documentset type='runonce'/><code event='options'><lotusscript>%REM
	Agent CSVImport
	Created Sep 12, 2013 by Mark Leusink/LinQed
	Description: Comments for Agent
%END REM
Option Public
Option Declare
Use "libImport"
UseLSX "*javacon"



</lotusscript></code><code event='declarations'><lotusscript>%REM
	Import Dailog Code
	Author: Teamstudio
	Description: Imports CSV file containing user / device information and creates 
	appropriate documents in the Unplugged Synchronizer.
	
	This code utilizes a modified version OpenCSV which is copyrighted by Bytecode Pty Ltd.
	and licensed under the Apache License, Version 2.0.
	Source code, along with copyright and license is available in the libImport
	Java script library in the au.com.bytecode.opencsv package.
	
%END REM

Dim importer As CSVImporter  
Dim availLicenses As Double

Private Const REQUIRED_IMPORT_HEADER_FIELDS =_
"UserName,Overwrite,Profiles,Active,CanCreateDevices,DevicePushRepLog"

Private Const ADDED = "New Users Imported"
Private Const OVERWRITTEN = "Existing Users, replaced"
Private Const IGNORED = "Existing Users, skipped"
Private Const DEV_ENABLED = "Enabled Devices added"
Private Const DEV_DISABLED = "Disabled Devices added"
Private Const DEV_EXISTED = "Existing Devices, skipped"

Private Const FLD_VALIDATE_STATUS = "ValidationStatus"
Private Const FLD_IMPORT_STATUS = "ImportStatus"
Private Const FLD_IMPORT_SUMMARY = "ImportSummary"
Private Const FLD_STATE = "State"

Const ERR_ADMN_ROLE_REQ = 13001
Const ERR_BAD_ROW = 13002
Const ERR_BAD_ROW_LEN = 13003
Const ERR_OVER_LICENSE = 13004
Const ERR_MISSING_HEADER_VALS = 13005
Const ERR_MISSING_HEADERS = 13006


Public Class CSVImporter
%REM
	Author: Teamstudio
	Description: Imports CSV file containing user / device information and creates 
	appropriate documents in the Unplugged Synchronizer.
	
	This code utilizes a modified version OpenCSV which is copyrighted by Bytecode Pty Ltd.
	and licensed under the Apache License, Version 2.0.
	Source code, along with copyright and license is available in the libImport
	Java script library in the au.com.bytecode.opencsv package.
%END REM
	Private s As NotesSession
	Private db As NotesDatabase
	Private vwDevicesAll As NotesView
	Private vwDevices As NotesView
	Private vwUsers As NotesView
	
	Private activeLicensesToCreate As Double
	Private lstImportStats List As Double
	Private logUsers As CSVWriter
	Private logDevices As CSVWriter
	Private map List As Integer
	Private sourcePath As String
	Private logDevicePath As String
	Private logUserPath As String
	
	Private docOpts As NotesDocument
	
	Private fileName As String
	
	Sub New
		
		Set s = New NotesSession
		Set db = s.currentdatabase
		Set vwUsers = db.Getview("UsersAll")
		Set vwDevices = db.Getview("DevicesEmbedded")
		Set vwDevicesAll = db.Getview("DevicesAllLU")
		
	End Sub
	
	Sub updateUI(Field As String, vVal As Variant)
		docOpts.ReplaceItemValue Field, vVal
	End Sub
	
	Sub uiValidateImport(doc As NotesDocument)
	
		Set docOpts = doc
		
		fileName = docOpts.Getitemvalue("Path")(0)
		
		If  fileName = "" then
			return
		End if
	
		updateUI "SourceFile", fileName
		sourcePath = fileName
		
		updateUI FLD_STATE, 2
		
		updateUI FLD_VALIDATE_STATUS,  "Validating file..."
		Sleep 1
		
		validateImport
		
		updateUI "UserLog", StrLeftBack(SourcePath, ".") + "_log_user.csv"
		updateUI "DeviceLog", StrLeftBack(SourcePath, ".")  + "_log_devices.csv"
		
		updateUI FLD_STATE, 3
		
	End Sub
	
	Sub validateImport()
		If Not UserHasAuthority Then
			Error ERR_ADMN_ROLE_REQ, "Only user's with Editor ACL access and the [Admin] ACL role can bulk import users"
		End If
		Dim c As New CSVReader
		c.loadFile(sourcePath)
		Dim i As Long
		Dim vArr As Variant
			If c.hasNext Then
			vArr = c.readNext()
			If Not IsArray(vArr) Then Error ERR_BAD_ROW, "Invalid input: row " &amp; i &amp;_
			" (" &amp; Join(vArr, ":") &amp; ")"
			If UBound(vArr) &lt; 5 Then  Error ERR_BAD_ROW_LEN, "Invalid input: row " &amp; i &amp;_
			" (" &amp; Join(vArr, ":") &amp; ")"
			
			createImportMap vArr, map
			
			updateUI FLD_VALIDATE_STATUS,  "Processed file header row..."
			
		End If
		
		Dim mapLen As Integer
		mapLen = UBound(vArr) 'save first row length to validate subsequent rows.
		
		activeLicensesToCreate = 0 'we need to reset this incase we're called more than once
		
		Do While c.hasNext
			i = i + 1
			vArr = c.readNext()
			If Not IsArray(vArr) Then 
				updateUI FLD_VALIDATE_STATUS,"Validation Failed"
				Error ERR_BAD_ROW, "Invalid input: row " &amp; i
			End If
			If UBound(vArr) &lt;&gt; mapLen Then
				updateUI FLD_VALIDATE_STATUS,"Validation Failed"
				Error ERR_BAD_ROW_LEN, "Invalid input (wrong number of elements), row: " &amp; i &amp;_
				" (" &amp; Join(vArr, ":") &amp; ")"
			End If
			
			If IsElement(map("Device_ID")) Then
				Dim devices As Variant
				devices = Split(vArr(map("Device_ID")), ";")
				ForAll pin In devices
					If "" &lt;&gt; FullTrim(pin) Then
						If "1" = vArr(map("Device_Active")) Then 
							If (getDeviceEntry(CStr(pin))Is Nothing) Then
								activeLicensesToCreate = activeLicensesToCreate + 1	
							End If
						End If
					End If
					If i Mod 20 = 0 Then updateUI FLD_VALIDATE_STATUS, "Validating row " &amp; i &amp; "..."
				End ForAll
			End If
			
		Loop
		
		c.close
		Delete c
		
		If activeLicensesToCreate &gt; availLicenses Then
			updateUI FLD_VALIDATE_STATUS, "Validated " &amp; i &amp; " rows. " &amp; activeLicensesToCreate &amp; " new devices; Exceeds " &amp; availLicenses &amp; " licenses available. "
		Else
			updateUI FLD_VALIDATE_STATUS, "Validated " &amp; i &amp; " rows. " &amp; activeLicensesToCreate &amp; " new devices will be activated.  Ready to import."
		End If
		
	End Sub
	
	Sub uiImport()
		
		logUserPath = docOpts.getItemValue("UserLog")(0)
		logDevicePath = docOpts.getItemValue("DeviceLog")(0)
		
		updateUI FLD_STATE, 4
		
		Dim msgSummary As String, nl As String
		nl = Chr(10)
		msgSummary =  nl &amp; _
		"Source File: " &amp; sourcePath &amp; nl &amp; _
		"User Log: " &amp; logUserPath &amp; nl &amp; _
		"Device Log: " &amp; logDevicePath &amp; nl
		
		updateUI FLD_IMPORT_SUMMARY,  msgSummary
		
		updateUI  FLD_IMPORT_STATUS,  "Preparing import of file: " &amp; sourcePath
		Sleep 1
		
		logSetup
		
		import
		
		ForAll stat In lstImportStats
			If stat &gt; 0 Then
				msgSummary  =  msgSummary &amp; nl &amp;   ListTag(stat) &amp; ":  "  &amp; stat
			End If
		End ForAll
		
		updateUI FLD_IMPORT_SUMMARY,  msgSummary
		
		updateUI FLD_STATE, 5
		
	End Sub
	
	Sub import()
		If Not UserHasAuthority Then
			docOpts.Replaceitemvalue "Status", "error"
			docOpts.Replaceitemvalue "StatusMsg", "Only user's with Editor ACL access and the [Admin] ACL role can bulk import users"
			Exit Sub
		End If
		
		If activeLicensesToCreate &gt; getLicenseCount() Then
			Error ERR_OVER_LICENSE, "Import would create " &amp;  activeLicensesToCreate &amp; " new devices; Exceeds " &amp; availLicenses &amp; " licenses available. "
		End If
		
		Dim vArr As Variant, i As Long
		'Import loop
		Dim c As New CSVReader
		i = 0
		Set c = New CSVReader
		c.loadFile(sourcePath)
		If c.hasNext Then
			vArr = c.readNext() 'skip headers 
		End If
		Do While c.hasNext
			i = i + 1
			If i Mod 20 = 0 Then updateUI FLD_IMPORT_STATUS, "Processing row " &amp; i &amp; "..."
			
			vArr = c.readNext()
			processRow vArr, map
		Loop
		
		updateUI  FLD_IMPORT_STATUS, "Processed " &amp; i &amp; " rows."
		
		c.close
		Delete c
		
		logCleanup
		
	End Sub
	
	Function processRow(vArr As Variant, map List As Integer) As Boolean
		
		Dim userName As NotesName
		Set userName = New NotesName(vArr(map("UserName")))
		
		Dim overwrite As Boolean
		overwrite = ("1" = vArr(map("Overwrite")))
		
		Dim create As Boolean, isOverwrite As Boolean
		create = True
		isOverwrite = False
		
		Dim dcUser As NotesDocumentCollection
		vwUsers.Refresh
		Set dcUser = vwUsers.getAllDocumentsByKey(userName.Abbreviated, True)
		
		Dim logAction As String
		Dim devicesAdded As String
		Dim devicesExisted As String
		
		If dcUser.count &gt; 0 Then
			'user exists
			If overwrite Then
				isOverwrite = True
				dcUser.removeAll True
				
				'		Uncommenting the following will also remove devices when overwrite is set;
				'		Note that if the device is used by another user it may affect that user too.
				'			Dim dcDevices As NotesDocumentCollection
				'			Set dcDevices = vwDevices.Getalldocumentsbykey(userName.Abbreviated, True)
				'			If dcDevices.count &gt; 0 Then
				'				dcDevices.removeall true
				'			End If
				
			Else
				IncrementStat(IGNORED)
				logAction = "Existed/Ignored"
				create = False
			End If
		End If
		
		If create Then
			Dim docUser As New NotesDocument(db)
			docUser.Form = "User"
			docUser.Replaceitemvalue "UserName", userName.Canonical
			Dim docDevices List As NotesDocument
			Dim fld As String, sVal As String
			
			' [, ",], generated by a space after a comma treated as content, can only be 
			' a mistake in our context, just strip it.
			Dim i As Integer
			For i = 0 To UBound(vArr)
				If { "} = vArr(i) Then  
					vArr(i) = ""
				End If
			Next
			
			ForAll el In map
				fld = ListTag(el)
				sVal = vArr(el)
				If StrComp("Profiles", fld, 1) =  0 Then
					docUser.replaceItemValue fld, Split(sVal, ";") 
				ElseIf StrComp("UserName", fld, 1) =  0 Then
					'nothing to do
				ElseIf StrComp("Device_", Left(fld, 7), 1) &lt;&gt; 0 Then 'Ignore device doc field prefix
					docUser.Replaceitemvalue fld, sVal
				End If
			End ForAll
			docUser.Save True, True
			
			
			If isOverwrite Then
				IncrementStat(OVERWRITTEN)
				logAction = "Overwritten"
			Else
				IncrementStat(ADDED)
				logAction = "Added"
			End If		
			
		End If 'create
		
		If IsElement(map("Device_ID")) Then
			
			Dim devices As Variant, existingDeviceCreatedFor As String, enabled As String
			devices = Split(vArr(map("Device_ID")), ";")
			ForAll pin In devices
				If "" &lt;&gt; FullTrim(pin) Then
					Dim veDevice As NotesViewEntry
					Set veDevice = getDeviceEntry(CStr(pin))
					If (veDevice Is Nothing) Then
						Set docDevices(pin) = New NotesDocument(db)
						docDevices(pin).replaceItemValue "ID", pin 
						docDevices(pin).Replaceitemvalue "Form", "Device" 
						docDevices(pin).Replaceitemvalue "UserName", userName.Canonical
						If devicesAdded = "" Then devicesAdded = pin Else devicesAdded = devicesAdded &amp; ";" &amp; pin
					Else
						IncrementStat(DEV_EXISTED)
						If devicesExisted = "" Then devicesExisted = pin Else devicesExisted = devicesExisted &amp; ";" &amp; pin
						Dim colvals As Variant
						colvals = veDevice.columnValues
						enabled = CStr(colvals(2))
						existingDeviceCreatedFor = CStr(colvals(1))
						If existingDeviceCreatedFor = userName.Canonical Then existingDeviceCreatedFor = ""
						writeDeviceLogLine  CStr(Pin), userName.Abbreviated, "Existed/Ignored", enabled, existingDeviceCreatedFor
					End If
				End If
			End ForAll	
			
			
			ForAll el In map
				fld = ListTag(el)
				sVal = vArr(el)
				If StrComp("Device_ID", fld, 1) =  0 Then
					'nothing to do
				ElseIf StrComp("Device_", Left(fld, 7), 1) = 0 Then 'device doc field prefix
					ForAll docDevice In docDevices
						docDevice.Replaceitemvalue Mid(fld, 8), sVal 
						If StrComp("Device_Active", fld, 1) =  0 Then
							If sVal = "1" Then
								IncrementStat(DEV_ENABLED)
								enabled = "1"
							Else
								IncrementStat(DEV_DISABLED)
								enabled = "0"
							End If
							writeDeviceLogLine docDevice.getItemValue("ID")(0), userName.Abbreviated, "Added", enabled, "" 
						End If
					End ForAll				
				End If
			End ForAll
			
			ForAll docDevice In docDevices
				docDevice.save True, True
			End ForAll
			
			Erase docDevices
			
		End If 'device
		
		writeUserLogLine userName.Abbreviated, logAction, devicesAdded, devicesExisted
	End Function
	
	Function createImportMap(vArr As Variant, out_map List As Integer) As Boolean
		Dim required As Variant, hasSome As Boolean, hasAll As Boolean
		required = Split(REQUIRED_IMPORT_HEADER_FIELDS, ",")
		Erase out_map
		Dim i As Integer
		For i = 0 To UBound(vArr)
			out_map(vArr(i)) = i
		Next
		hasAll = True
		ForAll req In required
			If ( IsElement( out_map(req) ) ) Then
				hasSome = True
			Else
				hasAll = False
			End If	
		End ForAll
		If hasSome And (Not hasAll) Then
			Error ERR_MISSING_HEADER_VALS, "CSV file header row does not contain all required fields: " &amp;_
			Chr(10) &amp; Chr(13) &amp; Chr(10) &amp; Chr(13) &amp; REQUIRED_IMPORT_HEADER_FIELDS
		ElseIf hasAll Then
			createImportMap = True
		Else
			Error ERR_MISSING_HEADERS, "CVS File does not define header row. Header row must contain: " &amp;_
			Chr(10) &amp; Chr(13) &amp; Chr(10) &amp; Chr(13) &amp; REQUIRED_IMPORT_HEADER_FIELDS
		End If
	End Function
	
	Function getLicenseCount() As Long
		Execute({Use "libLogin" :	availLicenses = getAvailableLicenses()})
		getLicenseCount = availLicenses
	End Function
	
	Function getDeviceEntry(deviceID As String) As NotesViewEntry
		vwDevicesAll.Refresh
		Set getDeviceEntry = vwDevicesAll.getEntryBykey(deviceID, True)
	End Function
	
	Sub IncrementStat(Stat As String)
		If(IsElement(lstImportStats(Stat))) Then
			lstImportStats(Stat) = lstImportStats(Stat) + 1
		Else
			'init stats in order to display
			If(Not IsElement(lstImportStats(ADDED))) Then lstImportStats(ADDED) = 0
			If(Not IsElement(lstImportStats(OVERWRITTEN))) Then lstImportStats(OVERWRITTEN) = 0
			If(Not IsElement(lstImportStats(IGNORED))) Then lstImportStats(IGNORED) = 0
			If(Not IsElement(lstImportStats(DEV_ENABLED))) Then lstImportStats(DEV_ENABLED) = 0
			If(Not IsElement(lstImportStats(DEV_DISABLED))) Then lstImportStats(DEV_DISABLED) = 0
			If(Not IsElement(lstImportStats(DEV_EXISTED))) Then lstImportStats(DEV_EXISTED) = 0
			
			lstImportStats(Stat) = 1
		End If
	End Sub
	
	Function UserHasAuthority() As Integer
		Dim sRole As String
		sRole = "[Admin]"
		UserHasAuthority = False
		Dim vHasRole As Variant, hasRole As Boolean
		If  "[" &lt;&gt; Left(sRole, 1) Then sRole = "[" &amp; sRole
		If  "]" &lt;&gt; Right(sRole, 1) Then sRole = sRole &amp; "]"
		vHasRole = Evaluate( {@IsMember("} &amp; sRole &amp; {"; @UserRoles)} )
		hasRole = -vHasRole(0)
		
		Dim s As New NotesSession
		UserHasAuthority = ( hasRole And (4 &lt;= s.Currentdatabase.Currentaccesslevel)  )
		
	End Function
	
	Sub logSetup() 
		
		ReDim logHeaders(3) As String
		logHeaders(0) = "UserName"
		logHeaders(1) = "ActionTaken"
		logHeaders(2) = "DevicesAdded"
		logHeaders(3) = "DevicesExisted"
		
		Set logUsers = New CSVWriter()
		logUsers.setOutputFile(logUserPath)
		logUsers.writeNext logHeaders
		
		ReDim logHeaders(4) As String
		logHeaders(0) = "DeviceId"
		logHeaders(1) = "ImportFileUserName"
		logHeaders(2) = "ActionTaken"
		logHeaders(3) = "Enabled"
		logHeaders(4) = "ExistingDeviceUser"
		
		Set logDevices = New CSVWriter()
		logDevices.setOutputFile(logDevicePath)
		logDevices.writeNext logHeaders
		
	End Sub
	
	Sub logCleanup()
		If Not logUsers Is Nothing Then
			logUsers.close
			Delete logUsers	
			Set logUsers = Nothing
		End If
		If Not logDevices Is Nothing Then
			logDevices.close
			Delete logDevices
			Set logDevices = Nothing
		End If
	End Sub
	
	Sub writeUserLogLine(userName As String, action As String, devicesAdded As String, devicesExisted As String)
		If Not logUsers Is Nothing Then
			ReDim logline(3) As String
			logline(0) = userName
			logline(1) = action
			logline(2) = devicesAdded
			logline(3) = devicesExisted
			logUsers.writeNext logline
		End If
	End Sub
	
	Sub writeDeviceLogLine(deviceID As String, ImportUserName As String, Action As String, Enabled As String,  ExistingDeviceUserName As String)
		If Not logDevices Is Nothing Then
			ReDim logline(4) As String
			logline(0) = deviceID
			logline(1) = ImportUserName
			logline(2) = Action
			logline(3) = Enabled
			logline(4) = ExistingDeviceUserName
			logDevices.writeNext logline
		End If
	End Sub
	
End Class

Public Class CSVWriter
%REM
	Author: Teamstudio
	Description: LS2J wrapper on CSV processing class.
	
	This code utilizes a modified version OpenCSV which is copyrighted by Bytecode Pty Ltd.
	and licensed under the Apache License, Version 2.0.
	Source code, along with copyright and license is available in the libImport
	Java script library in the au.com.bytecode.opencsv package.
%END REM
	Private jSession As JavaSession
	Private jClass As JavaClass
	Private jObject As JavaObject
	Private jError As JavaError
	Private methods List As JavaMethod
	Sub New
		Set jSession = New JavaSession()
		On Error GoTo errhandler
		Set jClass = jSession.GetClass("com/teamstudio/unplugged/libs/CsvWriterWrapper")  ' lib.Zip;
		Set jObject = jClass.CreateObject
		Set methods("setOutputFile") = jClass.GetMethod("setOutputFile", "(Ljava/lang/String;)V")
		Set methods("writeNext") = jClass.GetMethod("writeNext", "([Ljava/lang/String;)V")
		Set methods("close") = jClass.GetMethod("close", "()V")
		Exit Sub
errhandler:
		handleJError jSession.GetLastJavaError()
	End Sub
	
	Sub setOutputFile(filePath As String)
		methods("setOutputFile").Invoke jObject, filePath
		Exit Sub
errhandler:
		handleJError jSession.GetLastJavaError()
	End Sub
	
	Sub writeNext(strLine() As String)
		Dim vArr As Variant
		vArr = strLine
		methods("writeNext").Invoke  jObject, vArr
		Exit Sub
errhandler:
		handleJError jSession.GetLastJavaError()
	End Sub
	
	Sub Close()
		methods("close").Invoke jObject
		Exit Sub
errhandler:
		handleJError jSession.GetLastJavaError()
	End Sub
	
	Sub Delete
		ForAll m In methods
			If Not m Is Nothing Then 
				Delete m
				Set m = Nothing
			End If
		End ForAll
		If Not jObject Is Nothing Then
			Delete jObject
		End If
		'um, we don't do this because it apparently invalidates other instances!
		'		If Not jClass Is Nothing Then
		'			Delete jClass
		'		End If
	End Sub
	
	
	
	Private Function handleJError(je As JAVAERROR)
		Set jError = jSession.GetLastJavaError()
		If jError.errorMsg&lt;&gt;"" Then
			'Print "Stack Trace: " &amp;  jError.stackTrace
			Error Err, Err &amp; ": " &amp; Error() &amp; " [ " &amp; LSI_Info(2)  &amp; " - line " &amp; CStr(Erl) &amp; |]| 
		Else
			Error Err, Error() &amp; " [ " &amp; LSI_Info(12)  &amp; " - line " &amp; CStr(Erl) &amp; "]" 
		End If
		' Clear the Error
		jSession.ClearJavaError
		Exit Function
	End Function
End Class

Public Class CSVReader
%REM
	Author: Teamstudio
	Description: LS2J wrapper on CSV processing class.
	
	This code utilizes a modified version OpenCSV which is copyrighted by Bytecode Pty Ltd.
	and licensed under the Apache License, Version 2.0.
	Source code, along with copyright and license is available in the libImport
	Java script library in the au.com.bytecode.opencsv package.
%END REM
	Private jSession As JavaSession
	Private jClass As JavaClass
	Private jObject As JavaObject
	Private jError As JavaError
	Private methods List As JavaMethod
	Sub New
		Set jSession = New JavaSession()
		On Error GoTo errhandler
		Set jClass = jSession.GetClass("com/teamstudio/unplugged/libs/CsvReaderWrapper")  ' lib.Zip;
		Set jObject = jClass.CreateObject
		Set methods("loadFile") = jClass.GetMethod("loadFile", "(Ljava/lang/String;)V")
		Set methods("hasNext") = jClass.GetMethod("hasNext", "()Z")
		Set methods("readNext") = jClass.GetMethod("readNext", "()[Ljava/lang/String;")
		Set methods("close") = jClass.GetMethod("close", "()V")
		Exit Sub
errhandler:
		handleJError jSession.GetLastJavaError()
	End Sub
	
	Sub loadFile(filePath As String)
		methods("loadFile").Invoke jObject, filePath
		Exit Sub
errhandler:
		handleJError jSession.GetLastJavaError()
	End Sub
	
	Function readNext() As Variant
		readNext = methods("readNext").Invoke( jObject )
		Exit Function
errhandler:
		handleJError jSession.GetLastJavaError()
	End Function
	
	Function hasNext() As Boolean
		hasNext = methods("hasNext").Invoke( jObject )
		Exit Function
errhandler:
		handleJError jSession.GetLastJavaError()
	End Function
	
	Sub Close()
		methods("close").Invoke jObject
		Exit Sub
errhandler:
		handleJError jSession.GetLastJavaError()
	End Sub
	
	Sub Delete
		ForAll m In methods
			If Not m Is Nothing Then 
				Delete m
				Set m = Nothing
			End If
		End ForAll
		If Not jObject Is Nothing Then
			Delete jObject
		End If
		'um, we don't do this because it apparently invalidates other instances!
		'		If Not jClass Is Nothing Then
		'			Delete jClass
		'		End If
	End Sub
	
	
	
	Private Function handleJError(je As JAVAERROR)
		Set jError = jSession.GetLastJavaError()
		If jError.errorMsg&lt;&gt;"" Then
			'Print "Stack Trace: " &amp;  jError.stackTrace
			Error Err, Err &amp; ": " &amp; Error() &amp; " [ " &amp; LSI_Info(2)  &amp; " - line " &amp; CStr(Erl) &amp; |]| 
		Else
			Error Err, Error() &amp; " [ " &amp; LSI_Info(12)  &amp; " - line " &amp; CStr(Erl) &amp; "]" 
		End If
		' Clear the Error
		jSession.ClearJavaError
		Exit Function
	End Function
End Class

Class TestCSVImporter As CSVImporter
%REM
	Author: Teamstudio
	Description: Tests Import of CSV file containing user / device information.
	
	This code utilizes a modified version OpenCSV which is copyrighted by Bytecode Pty Ltd.
	and licensed under the Apache License, Version 2.0.
	Source code, along with copyright and license is available in the libImport
	Java script library in the au.com.bytecode.opencsv package.
%END REM
	Private headersFull As String
	Private headersUsers As String
	Private importFile As CSVWriter
	Private keepLog As Boolean
	Private logListDevices List As Variant
	Private logListUsers List As Variant
	Sub New
		headersFull = "UserName,Overwrite,Profiles,Active,CanCreateDevices,DevicePushRepLog,Device_ID,Device_Active"
		headersUsers = "UserName,Overwrite,Profiles,Active,CanCreateDevices,DevicePushRepLog"
		Dim ws As New NotesUIWorkspace
		Set db = ws.currentdatabase.database
	End Sub
	
	Private Sub createCsv(sPath As String, sHeaders As String)
		Dim vArr As Variant, i As Integer
		vArr = Split(sHeaders, ",")
		ReDim sArr(UBound(vArr)) As String
		For i = 0 To UBound( vArr )
			sArr(i) = vArr(i)
		Next
		Set importFile = New CSVWriter()
		importFile.setOutputFile sPath
		importFile.writeNext sArr
	End Sub
	
	Private Sub writeInputLine(sArr() As String)
		importFile.writeNext sArr
	End Sub
	
	Private Sub closeCsv()
		If Not importFile Is Nothing Then
			importFile.close
			Delete importFile
			Set importFile = Nothing
		End If
	End Sub
	
	Private Sub clearDb()
		db.Alldocuments.Removeall True
	End Sub
	
	Private Function getDevice(device As String) As NotesDocument
		Set getDevice = _
		db.Search({Form = "Device" &amp; ID = "} &amp; device &amp; {"}, Nothing, 0).Getfirstdocument()
	End Function
	
	Private Function getUser(userName As String) As NotesDocument
		Set getUser = _
		db.Search({Form = "User" &amp; UserName = "} &amp; userName &amp; {"}, Nothing, 0).Getfirstdocument()
	End Function
	
	Private Sub Assert(msg As String)
		Error 9999, GetThreadInfo(10) &amp; ": "  &amp; msg
	End Sub
	
	Private Sub AssertFieldEquals(doc As NotesDocument, sField As String, sVal As String)
		Dim sValActual As String
		sValActual = doc.Getitemvalue(sField)(0)
		If sVal &lt;&gt; sValActual Then
			Error 9999, GetThreadInfo(10) &amp; ": Expected: "  &amp; sVal &amp; "; Actual: " &amp; sValActual &amp; " (" &amp; sField &amp; "/" &amp; doc.Getitemvalue("Username")(0) &amp; ")"
		End If
	End Sub
	
	
	Private Sub writeUserLogLine(userName As String, action As String, devicesAdded As String, devicesExisted As String)
		If keepLog Then
			Dim logEntry List As String
			logEntry("userName") = userName
			logEntry("action") = action
			logEntry("devicesAdded") = devicesAdded
			logEntry("devicesExisted") = devicesExisted
			logListUsers(userName) = logEntry
		End If
	End Sub
	
	Private Sub writeDeviceLogLine(deviceID As String, ImportUserName As String, Action As String, Enabled As String,  ExistingDeviceUserName As String)
		If keepLog Then
			Dim logEntry List As String
			logEntry("deviceID") = deviceID
			logEntry("ImportUserName")  = ImportUserName
			logEntry("Action")  = Action
			logEntry("Enabled")  = Enabled
			logEntry("ExistingDeviceUserName")  = ExistingDeviceUserName
			logListDevices(deviceID) = logEntry
		End If
	End Sub
	
	Private Sub setup()
		Print "Setup test: " &amp; GetThreadInfo(10)
		
		If db.Alldocuments.Count &gt; 0 Then
			'Make sure someone doesn't run this in a db where they care about the data (e.g. production!)
			Error 9999, "Tests cannot be run on databases containing documents.  Tests require deletion of all data.  Use an empty test database to run tests."
		End If
		
		clearDb
		
		keepLog = False
		sourcePath = "testImportCsv.csv"
	End Sub
	
	Private Sub teardown()
		clearDb
		keepLog = False
		Erase logListDevices
		Erase logListUsers
		Print "Teardown test: " &amp; GetThreadInfo(10)
	End Sub
	
	Sub runTests()	
		
		testOverLicense
		testImport
		testNoDeviceColumnImport
		testNoDeviceRowsImport
		testOverwriteImport
		testImportWontOverWriteDevices
		testBadRow
		testLogging
		
		MsgBox "Tests passed"
		Exit Sub
	End Sub
	
	Private Sub testImport()
		setup
		
		createCsv sourcePath, headersFull &amp; ",spareUserFld,Device_spareFld"
		Dim i As Integer
		For i = 0 To 9
			ReDim sLine(9) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			sLine(6) = "210000_" &amp; i	'Device_ID,
			sLine(7) = CStr(i)			'Device_Active
			sLine(8) = "userSpare_" &amp; i	'spareUserFld  -- test adding addl data to user
			sLine(9) = "deviceSpare_" &amp; i	'Device_spareFld  -- test adding addl data to device
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 0 To 9
			Dim docUser As NotesDocument
			Set docUser = getUser("User_" &amp; i)
			If docUser Is Nothing Then
				Assert "User not created: User_" &amp; i
			Else
				AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i 
				AssertFieldEquals docUser, "Active", CStr(i)
				AssertFieldEquals docUser, "CanCreateDevices", CStr(i) 
				AssertFieldEquals docUser, "DevicePushRepLog", CStr(i)
				AssertFieldEquals docUser, "spareUserFld", "userSpare_" &amp; i 
			End If
			
			Dim docDevice As NotesDocument
			Set docDevice = getDevice("210000_" &amp; i)
			If docDevice Is Nothing Then
				Assert "Device not created: 210000_" &amp; i
			Else
				AssertFieldEquals docDevice, "Username", "User_" &amp; i
				AssertFieldEquals docDevice, "Active", CStr(i)
				AssertFieldEquals docDevice, "spareFld", "deviceSpare_" &amp; i 
			End If
		Next
		teardown
	End Sub
	
	Private Sub testNoDeviceColumnImport()
		setup
		
		createCsv sourcePath, headersUsers
		Dim i As Integer
		For i = 0 To 9
			ReDim sLine(5) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		Dim dc As NotesDocumentCollection
		Set dc = db.Search({Form = "Device"}, Nothing, 0)
		If dc.Count &gt; 0 Then
			Error 9999, GetThreadInfo(1) &amp; ":" &amp;  " Devices documents were created and should not have been!"
		End If
		
		For i = 0 To 9
			Dim docUser As NotesDocument
			Set docUser = getUser("User_" &amp; i)
			If docUser Is Nothing Then
				Assert "User not created: User_" &amp; i
			Else
				AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i 
				AssertFieldEquals docUser, "Active",CStr(i)
				AssertFieldEquals docUser, "CanCreateDevices", CStr(i) 
				AssertFieldEquals docUser, "DevicePushRepLog", CStr(i)
			End If
		Next
		teardown
	End Sub
	
	Private Sub testNoDeviceRowsImport()
		setup
		
		createCsv sourcePath, headersFull
		Dim i As Integer
		For i = 0 To 9
			ReDim sLine(7) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			If( i Mod 2 = 0) Then
				sLine(6) = "210000_" &amp; i	'Device_ID,
				sLine(7) = CStr(i)			'Device_Active
			End If
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 0 To 9
			Dim docUser As NotesDocument
			Set docUser = getUser("User_" &amp; i)
			If docUser Is Nothing Then
				Assert "User not created: User_" &amp; i
			Else
				AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i 
				AssertFieldEquals docUser, "Active",CStr(i)
				AssertFieldEquals docUser, "CanCreateDevices", CStr(i) 
				AssertFieldEquals docUser, "DevicePushRepLog", CStr(i)
			End If
			
			Dim docDevice As NotesDocument
			Set docDevice = getDevice("210000_" &amp; i)
			If docDevice Is Nothing Then
				If( i Mod 2 = 0) Then
					Assert "Device not created: 210000_" &amp; i					
				End If
			Else
				If( i Mod 2 &lt;&gt; 0) Then
					Assert "Device SHOULD NOT have been created: 210000_" &amp; i					
				End If
				AssertFieldEquals docDevice, "Username", "User_" &amp; i
				AssertFieldEquals docDevice, "Active", CStr(i)
			End If
			
		Next
		teardown
	End Sub
	
	Private Sub testOverwriteImport()
		setup
		
		createCsv sourcePath, headersUsers
		Dim i As Integer
		For i = 0 To 9
			ReDim sLine(5) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 0 To 9
			Dim docUser As NotesDocument
			Set docUser = getUser("User_" &amp; i)
			If docUser Is Nothing Then
				Assert "User not created: User_" &amp; i
			Else
				AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i 
				AssertFieldEquals docUser, "Active",CStr(i)
				AssertFieldEquals docUser, "CanCreateDevices", CStr(i) 
				AssertFieldEquals docUser, "DevicePushRepLog", CStr(i)
			End If
		Next
		
		
		createCsv sourcePath, headersUsers
		For i = 0 To 9
			ReDim sLine(5) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = CStr(i Mod 2)	'Overwrite, -- MOD 2!
			sLine(2) = "profile_" &amp; i + 99	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 0 To 9
			Set docUser = getUser("User_" &amp; i)
			If docUser Is Nothing Then
				Assert "User not created: User_" &amp; i
			Else
				If ( i Mod 2 = 1 ) Then 'overwrite = false in last loop
					AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i + 99
				Else
					AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i 
				End If
				
				AssertFieldEquals docUser, "Active",CStr(i)
				AssertFieldEquals docUser, "CanCreateDevices", CStr(i) 
				AssertFieldEquals docUser, "DevicePushRepLog", CStr(i)
			End If
		Next
		
		teardown
	End Sub
	
	Private Sub testImportWontOverWriteDevices()
		setup
		
		createCsv sourcePath, headersFull
		Dim i As Integer
		For i = 0 To 3
			ReDim sLine(7) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			sLine(6) = "210000_" &amp; (3-i)	'Device_ID,
			sLine(7) = "0"			'Device_Active
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 0 To 3
			Dim docUser As NotesDocument
			Set docUser = getUser("User_" &amp; i)
			If docUser Is Nothing Then
				Assert "User not created: User_" &amp; i
			Else
				AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i 
				AssertFieldEquals docUser, "Active", CStr(i)
				AssertFieldEquals docUser, "CanCreateDevices", CStr(i) 
				AssertFieldEquals docUser, "DevicePushRepLog", CStr(i) 
			End If
			
			Dim docDevice As NotesDocument
			Set docDevice = getDevice("210000_"  &amp; (3-i))
			If docDevice Is Nothing Then
				Assert "Device not created: 210000_"  &amp; (3-i)
			Else
				AssertFieldEquals docDevice, "Username", "User_" &amp; i
				AssertFieldEquals docDevice, "Active", "0"
			End If
		Next
		
		createCsv sourcePath, headersFull
		For i = 0 To 3
			ReDim sLine(7) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i + 99	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			sLine(6) = "210000_" &amp; (i)	'Device_ID,
			sLine(7) = "0"		'Device_Active
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 0 To 3
			
			Set docUser = getUser("User_" &amp; i)
			If docUser Is Nothing Then
				Assert "User not created: User_" &amp; i
			Else
				'make sure we overwrote the users
				AssertFieldEquals docUser, "Profiles",  "profile_"  &amp; i + 99
			End If
			
			Set docDevice = getDevice("210000_"  &amp; (i))
			If docDevice Is Nothing Then
				Assert "Device not created: 210000_"  &amp; (i)
			Else
				' if we didn't overwrite device, it should still be orig users
				AssertFieldEquals docDevice, "Username", "User_" &amp; (3-i)
			End If
		Next
		
		teardown
	End Sub
	
	Private Sub testOverLicense()
		setup
		
		createCsv sourcePath, headersFull
		Dim i As Integer
		For i = 0 To 3
			ReDim sLine(7) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			sLine(6) = "210000_" &amp; (i)	'Device_ID,
			sLine(7) = "1"			'Device_Active
			writeInputLine sLine
		Next
		closeCsv
		validateImport
		
		On Error Resume Next
		
		import
		
		On Error GoTo 0
		
		If Err &lt;&gt; ERR_OVER_LICENSE Then
			Error 9999, "Import didn't catch over-licensing condition!"
		End If
		Err = 0 
		
		
		teardown
	End Sub
	
	Private Sub testBadRow()
		setup
		
		createCsv sourcePath, headersFull
		Dim i As Integer
		For i = 0 To 2
			ReDim sLine(6) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			'sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(2) = CStr(i)			'Active,'
			sLine(3) = CStr(i)			'CanCreateDevices,
			sLine(4) = CStr(i)			'DevicePushRepLog,
			sLine(5) = "210000_" &amp; (i)	'Device_ID,
			sLine(6) = "1"			'Device_Active
			writeInputLine sLine
		Next
		closeCsv
		
		On Error Resume Next
		validateImport
		
		On Error GoTo 0
		
		If Err &lt;&gt; ERR_BAD_ROW_LEN Then
			Error 9999, "Import didn't catch invalid row!"
		End If
		Err = 0 
		
		
		teardown
	End Sub
	
	Private Sub testLogging()
		setup
		
		createCsv sourcePath, headersFull
		Dim i As Integer
		For i = 0 To 3
			ReDim sLine(7) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			sLine(6) = "210000_" &amp; (i)	'Device_ID,
			sLine(7) = "0"			'Device_Active
			writeInputLine sLine
		Next
		closeCsv
		
		keepLog = True
		
		validateImport
		import
		
		Dim user As String, device As String, userLog As Variant, deviceLog As Variant
		
		'Verify straight import
		For i = 1 To 3
			user = "User_" &amp; i
			device = "210000_" &amp; (i)
			If Not IsElement(logListUsers(user)) Then Error 9999, "Failed to log user: " &amp; user
			If Not IsElement(logListDevices(device)) Then Error 9999, "Failed to log device: " &amp; device
			
			userLog = logListUsers(user)
			deviceLog =logListDevices(device)
			
			If ( userLog("action") &lt;&gt; "Added" ) Then Error 9999, "Action should be 'Added': " &amp; user
			If ( userLog("devicesAdded") &lt;&gt; device ) Then Error 9999, "devicesAdded should be '" &amp; device &amp; "': " &amp; user
			If ( userLog("devicesExisted") &lt;&gt; "" ) Then Error 9999, "devicesExisted should be '': " &amp; user
			
			If ( deviceLog("ImportUserName") &lt;&gt; user ) Then Error 9999, "ImportUserName should be '" &amp; user &amp; "': " &amp; user
			If ( deviceLog("Action") &lt;&gt; "Added" ) Then Error 9999, "Action should be 'Added': " &amp; user
			If ( deviceLog("Enabled") &lt;&gt; "0" ) Then Error 9999, "Enabled should be '0': " &amp; user
			If ( deviceLog("ExistingDeviceUserName") &lt;&gt; "" ) Then Error 9999, "ExistingDeviceUserName should be '': " &amp; user
			
		Next
		
		Erase logListUsers
		Erase logListDevices
		
		'Verify overwrite import, existing devices
		createCsv sourcePath, headersFull
		For i = 0 To 3
			ReDim sLine(7) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			sLine(6) = "210000_" &amp; (3-i)	'Device_ID,
			sLine(7) = "0"			'Device_Active
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 1 To 3
			user = "User_" &amp; i
			device = "210000_" &amp; (3-i)
			If Not IsElement(logListUsers(user)) Then Error 9999, "Failed to log user: " &amp; user
			If Not IsElement(logListDevices(device)) Then Error 9999, "Failed to log device: " &amp; device
			
			userLog = logListUsers(user)
			deviceLog =logListDevices(device)
			
			If ( userLog("action") &lt;&gt; "Overwritten" ) Then Error 9999, "Action should be 'Overwritten': " &amp; user
			If ( userLog("devicesAdded") &lt;&gt; "" ) Then Error 9999, "devicesAdded should be '': " &amp; user
			If ( userLog("devicesExisted") &lt;&gt; device ) Then Error 9999, "devicesExisted should be '" &amp; device &amp; "': " &amp; user
			
			If ( deviceLog("ImportUserName") &lt;&gt; user ) Then Error 9999, "ImportUserName should be '" &amp; user &amp; "': " &amp; user
			If ( deviceLog("Action") &lt;&gt; "Existed/Ignored" ) Then Error 9999, "Action should be 'Existed/Ignored': " &amp; user
			If ( deviceLog("Enabled") &lt;&gt; "0" ) Then Error 9999, "Enabled should be '0': " &amp; user
			If ( deviceLog("ExistingDeviceUserName") = "" ) Then Error 9999, "ExistingDeviceUserName should have value: " &amp; user
		Next
		
		'Verify ignore existing import, add devices
		createCsv sourcePath, headersFull
		For i = 0 To 3
			ReDim sLine(7) As String
			sLine(0) = "User_" &amp; i		'UserName,
			sLine(1) = "1"				'Overwrite,
			sLine(2) = "profile_" &amp; i	'Profiles,
			sLine(3) = CStr(i)			'Active,'
			sLine(4) = CStr(i)			'CanCreateDevices,
			sLine(5) = CStr(i)			'DevicePushRepLog,
			sLine(6) = "210000_" &amp; (3-i) &amp; ";210000_" &amp; (39-i)	'Device_ID,
			sLine(7) = "0"			'Device_Active
			writeInputLine sLine
		Next
		closeCsv
		
		validateImport
		import
		
		For i = 1 To 3
			Dim device2 As String, deviceLog2 As Variant
			user = "User_" &amp; i
			device = "210000_" &amp; (3-i)
			device2 = "210000_" &amp; (39-i)
			If Not IsElement(logListUsers(user)) Then Error 9999, "Failed to log user: " &amp; user
			If Not IsElement(logListDevices(device)) Then Error 9999, "Failed to log device: " &amp; device
			If Not IsElement(logListDevices(device2)) Then Error 9999, "Failed to log device: " &amp; device2
			
			userLog = logListUsers(user)
			deviceLog =logListDevices(device)
			deviceLog2 =logListDevices(device2)
			
			If ( userLog("action") &lt;&gt; "Overwritten" ) Then Error 9999, "Action should be 'Overwritten': " &amp; user
			If ( userLog("devicesAdded") &lt;&gt; device2 ) Then Error 9999, "devicesAdded should be '" &amp; device2 &amp; "': " &amp; user
			If ( userLog("devicesExisted") &lt;&gt; device ) Then Error 9999, "devicesExisted should be '" &amp; device &amp; "': " &amp; user
			
			If ( deviceLog("ImportUserName") &lt;&gt; user ) Then Error 9999, "ImportUserName should be '" &amp; user &amp; "': " &amp; user
			If ( deviceLog("Action") &lt;&gt; "Existed/Ignored" ) Then Error 9999, "Action should be 'Existed/Ignored': " &amp; user
			If ( deviceLog("Enabled") &lt;&gt; "0" ) Then Error 9999, "Enabled should be '0': " &amp; user
			If ( deviceLog("ExistingDeviceUserName") = "" ) Then Error 9999, "ExistingDeviceUserName should have value: " &amp; user
			
			If ( deviceLog2("ImportUserName") &lt;&gt; user ) Then Error 9999, "ImportUserName should be '" &amp; user &amp; "': " &amp; user
			If ( deviceLog2("Action") &lt;&gt; "Added" ) Then Error 9999, "Action should be 'Added': " &amp; user
			If ( deviceLog2("Enabled") &lt;&gt; "0" ) Then Error 9999, "Enabled should be '0': " &amp; user
			If ( deviceLog2("ExistingDeviceUserName") &lt;&gt; "" ) Then Error 9999, "ExistingDeviceUserName should be '': " &amp; user
			
		Next
		
		teardown
	End Sub
	
End Class
</lotusscript></code><code event='initialize'><lotusscript>Sub Initialize
	
	Dim importer As CSVImporter
	Set importer = New CSVImporter()
	
	Dim s As New NotesSession
	Dim docContext As NotesDocument
	Set docContext = s.DocumentContext
	
	Call importer.uiValidateImport( docContext )
	Call importer.uiImport() 
	
End Sub


</lotusscript></code>
<rundata processeddocs='0' exitcode='0'>
<agentmodified><datetime dst='true'>20130913T002724,89+02</datetime></agentmodified></rundata></agent>

